@top SwimProgramme { statement* }

ConstantDefinition {
    setKeyword ConstantName expression
}

ConstantName { identifier }

expression {
    Number | string | Boolean
}

string {
    '"' StringContent '"'
}

Boolean {
  identifier
}

PaceDefinition {
    paceKeyword PaceDefinitionName "=" Pace
}

PaceDefinitionName { identifier }

intensity {
    percentage | PaceAlias
}

Pace {
    intensity ("->" intensity)?
}

percentage {
    Number "%"         // e.g., 60% or 5%
}

PaceAlias { identifier }  // a named pace, e.g., easy or hard

statement {
    instruction
    | ConstantDefinition
    | PaceDefinition
}

instruction {
    SwimInstruction
    | RestInstruction
    | messageSpecification
}

SwimInstruction {
    (Number repititionOperator)?
    (SingleInstruction | BlockInstruction) StrokeModifier? instructionModifier*
}

SingleInstruction {
    distance Stroke
}

distance { Number }

Stroke { identifier }

BlockInstruction {
    "{" instruction+ "}"
}

StrokeModifier { identifier }

instructionModifier {
  EquipmentSpecification | paceSpecification | timeSpecification
}

EquipmentSpecification {
    "+" EquipmentName+
}

EquipmentName { identifier }

paceSpecification {
    "@" Pace
}

timeSpecification {
    onKeyword Duration
}

Duration {
    Number ":" Number    // minutes:seconds
}

RestInstruction {
    Duration restKeyword
}

messageSpecification {
  ">" Message
}

@tokens {
    setKeyword            { "set" }
    paceKeyword           { "pace" }
    restKeyword           { "rest" }
    onKeyword             { "on" }
    space       { @whitespace+ }
    Comment     { "#" ![\n]* }
    Number      { @digit+ }
    Message     { ![\n]+ }
    StringContent { (!["\\] | "\\" _)+ }
    identifier  { @asciiLetter+ }
    repititionOperator { "x" }
    @precedence { StringContent, space }
    @precedence { StringContent, Comment }
    @precedence { repititionOperator, identifier }
    @precedence { paceKeyword, identifier }
    @precedence { setKeyword, identifier }
    @precedence { onKeyword, identifier }
    @precedence { space, Message }
    @precedence { Comment, Message }
}

@skip { space | Comment }
