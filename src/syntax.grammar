@top SwimProgramme {
    PoolLengthDeclaration
    DistanceUnitDeclaration
    PaceDefinition*
    Statement+
}

PoolLengthDeclaration {
    "set PoolLength" Number LengthUnit
}

LengthUnit { identifier }

DistanceUnitDeclaration {
    "set DistanceUnit" DistanceUnit
}

DistanceUnit { identifier }

PaceDefinition {
    "Pace" PaceAlias "=" Percentage
}

Percentage {
    Number "%"         // e.g., 60% or 5%
}

Statement {
  Instruction
  | RepeatedInstruction
  | RestInstruction
  | Message
}

Instruction {
  (SingleInstruction | BlockInstruction) StrokeType? GearSpecification? Pace?
}

SingleInstruction {
    Distance Stroke
}

Distance { Number }

Stroke { identifier }

BlockInstruction {
  "{" Statement+ "}"
}

StrokeType { identifier }

GearSpecification {
    "+" RequiredGear+
}

RequiredGear { identifier }

Pace {
    "@" ( Duration 
        | PerceivedRate
        | VariableRate )
}

Duration {
    Number ":" Number    // minutes:seconds
}

PerceivedRate {
    (Percentage | PaceAlias)
}

PaceAlias { identifier }  // a named pace, e.g., easy or hard

VariableRate {
     PerceivedRate "->" PerceivedRate  // can model increasing or decreasing rate
}

RepeatedInstruction {
    Number repititionOperator Instruction
}

RestInstruction {
    Duration "rest"
}

@tokens {
    space       { @whitespace+ }
    Comment     { "#" ![\n]* }
    Number      { @digit+ }
    Message     { ">" ![\n]+ }
    identifier  { @asciiLetter+ }
    repititionOperator {"x"}
    @precedence { repititionOperator, identifier }
}

@skip { space | Comment }
