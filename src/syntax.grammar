@top StatementList { Statement+ }

Statement {
  Set
  | RestInstruction
  | PaceDefinition
  | Comment
}

Set {
  Instruction | RepeatedInstruction
}

Instruction {
  (SingleInstruction | BlockInstruction) StrokeType? GearSpecification? Pace?
}

BlockInstruction {
  "{" Statement+ "}"
}

RepeatedInstruction {
    Number "x" Instruction
}

SingleInstruction {
    Distance Stroke
}

StrokeType {
    "Kick" | "Pull" | "Drill"
}

GearSpecification {
    "+" RequiredGear+
}

RequiredGear {
    "Fins"
    | "Board"
    | "Paddles"
    | "Bouy"
}

RestInstruction {
    Duration "rest"
}

Distance {
    Number
}

Stroke {
    "Freestyle"
  | "Backstroke"
  | "Butterfly"
  | "Breaststroke"
}

PaceDefinition {
    "Pace" Identifier "=" Percentage
}

Pace {
    "@" ( FixedTime 
        | PerceivedRate
        | VariableRate )
}

FixedTime {
    Duration
}

Duration {
    Number ":" Number    // minutes:seconds
}

Percentage {
    Number "%"         // e.g., 60% or 5%
}

PerceivedRate {
    (Percentage | PaceAlias)
}

VariableRate {
     PerceivedRate "->" PerceivedRate  // can model increasing or decreasing rate
}

PaceAlias {
    Identifier        // a named pace, e.g., easy or hard
}

@tokens {
    space      { @whitespace+ }
    Comment    { "#" ![\n]* }
    Number     { @digit+ }
    Identifier { @asciiLetter+ }
}

@skip { space }
